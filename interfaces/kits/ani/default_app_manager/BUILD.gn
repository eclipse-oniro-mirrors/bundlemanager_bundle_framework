# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//foundation/bundlemanager/bundle_framework/appexecfwk.gni")

ohos_shared_library("ani_default_app_manager") {
  shlib_type = "ani"
  branch_protector_ret = "pac_ret"

  sanitize = {
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
    integer_overflow = true
    ubsan = true
  }

  include_dirs = [
    "${inner_api_path}/appexecfwk_core/include",
    "${kits_path}/ani/common",
    "${kits_path}/js/common",
  ]
  defines = [
    "APP_LOG_TAG = \"BMS\"",
    "LOG_DOMAIN = 0xD001120",
  ]

  cflags = [
    "-Os",
    "-fstack-protector-strong",
  ]

  cflags_cc = [
    "-Os",
    "-fstack-protector-strong",
  ]

  deps = [
    "${base_path}:appexecfwk_base",
    "${common_path}:libappexecfwk_common",
    "${core_path}:appexecfwk_core",
    "${kits_path}/ani/common:bms_ani_common",
    "${kits_path}/js/common:bundle_napi_common",
  ]

  external_deps = [
    "ability_base:want",
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_core",
    "napi:ace_napi",
    "runtime_core:ani",
    "runtime_core:ani_helpers",
    "samgr:samgr_proxy",
  ]

  if (bundle_framework_default_app) {
    sources = [ "ani_default_app_manager.cpp" ]
    defines += [ "BUNDLE_FRAMEWORK_DEFAULT_APP" ]
  } else {
    sources = [ "ani_default_app_manager_unsupported.cpp" ]
  }

  if (bundle_framework_graphics) {
    defines += [ "BUNDLE_FRAMEWORK_GRAPHICS" ]
  }

  subsystem_name = "bundlemanager"
  part_name = "bundle_framework"
}

generate_static_abc("default_app_manager") {
  base_url = "./ets"
  files = [ "./ets/@ohos.bundle.defaultAppManager.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/default_app_manager.abc"
}

ohos_prebuilt_etc("default_app_manager_etc") {
  source = "$target_out_dir/default_app_manager.abc"
  module_install_dir = "framework"
  subsystem_name = "bundlemanager"
  part_name = "bundle_framework"
  deps = [ ":default_app_manager" ]
}
